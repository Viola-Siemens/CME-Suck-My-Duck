plugins {
    id 'java'
}

group = 'com.hexagram2021.cme_suck_my_duck'
version = '1.0.1'

compileJava {
    sourceCompatibility = 8
    targetCompatibility = 8
}

java.disableAutoTargetJvm()

repositories {
    mavenCentral()
    maven {
        name = 'minecraft'
        url = 'https://libraries.minecraft.net/'
        group = 'com.mojang'
    }
    maven {
        name = 'Fabric'
        url = 'https://maven.fabricmc.net/'
        group = 'net.fabricmc'
    }
    maven {
        name = 'Forge'
        url = 'https://maven.minecraftforge.net'
        group = 'net.minecraftforge'
    }
    maven {
        name = 'NeoForge'
        url = 'https://maven.neoforged.net/releases'
        group = 'net.neoforged.fancymodloader'
    }
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'org.ow2.asm:asm:9.6'

    compileOnly 'net.fabricmc:fabric-loader:0.15.11'
    compileOnly 'net.minecraftforge:javafmllanguage:1.20.1-47.2.0'
    compileOnly 'net.neoforged.fancymodloader:loader:2.0.20'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

sourceSets {
    main {
        resources {
            srcDirs += 'src/main/resources'
        }
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"       : "CME Suck My Duck",
                "Specification-Vendor"      : "Hexagram",
                "Premain-Class"             : "com.hexagram2021.cme_suck_my_duck.CMESuckMyDuck",
                "Main-Class"                : "com.hexagram2021.cme_suck_my_duck.CMESuckMyDuck",
                "Can-Redefine-Classes"      : "true",
                "Can-Retransform-Classes"   : "true"
        ])
    }
    from "LICENSE"
}

test {
    useJUnitPlatform()
}

tasks.processResources {
    filesMatching("*.json") {
        expand (["version": project.version])
    }
    filesMatching("META-INF/*.toml") {
        expand (["version": project.version])
    }
}